plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'kotlin-parcelize'
}

android {
	compileSdk 31

	defaultConfig {
		applicationId "com.cube.fusion.android.demoapp"
		minSdk 23
		targetSdk 31
		versionCode 1
		versionName "1.0"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = '1.8'
	}
	buildFeatures {
		viewBinding true
		buildConfig true
	}

	flavorDimensions "samerepo"
	productFlavors {
		local {
			dimension "samerepo"
			buildConfigField "boolean", "USING_LOCAL_REPO", "true"
		}
		jitpack {
			dimension "samerepo"
			buildConfigField "boolean", "USING_LOCAL_REPO", "false"
		}
	}
}

dependencies {
	// AndroidX dependencies
	implementation 'androidx.core:core-ktx:1.7.0'
	implementation 'androidx.appcompat:appcompat:1.4.1'

	// Material design dependency
	implementation 'com.google.android.material:material:1.5.0'

	// Lifecycle KTX
	implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'

	// Fusion dependencies - specify version in the root build.gradle
	implementation "com.github.3sidedcube.Android-Fusion-Core:core:${project.fusionLibVersion}"
	implementation "com.github.3sidedcube.Android-Fusion-Core:populator-coroutine-source-cache:${project.fusionLibVersion}"
	implementation "com.github.3sidedcube.Android-Fusion-Core:populator-retrofit:${project.fusionLibVersion}"

	// Same-repo Fusion dependencies
	def currentLibVersion = "v0.0.4-alpha1"
	localImplementation project(":core")
	localImplementation project(":activity")
	jitpackImplementation "com.github.3sidedcube.Android-Fusion-AndroidUi:core:$currentLibVersion"
	jitpackImplementation "com.github.3sidedcube.Android-Fusion-AndroidUi:activity:$currentLibVersion"

	// Image loading/populating
	implementation "io.coil-kt:coil:2.0.0"

	// Parsing of JSON data with Jackson
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.11.1'
}